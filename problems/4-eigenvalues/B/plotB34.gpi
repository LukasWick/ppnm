set term svg font "Arial,20" size 1024,600 background rgb "white"
set out "PlotB34.svg"
set xlabel "n"
set ylabel "t [ms]"
set title "Evaluation time for the different methods"
set key bottom right
set xrange [10:500]


set log x;
set log y;


a1 = 3
b1 = 10
a2 = 3
b2 = 10
a3 = 3
b3 = 10
c1 = 1
c2 = 1
c3 = 1 

f(x) = log10((x/b1)**a1 + c1)
g(x) = log10((x/b2)**a2 + c2)
h(x) = log10((x/b3)**a3 + c3) 
set fit quiet
fit f(x) "out.plotB34vbv1.data" using 1:(log10($2)) via b1,a1,c1
fit g(x) "out.plotB34vbvn.data" using 1:(log10($2)) via b2,a2,c2
fit h(x) "out.plotB34cyclic.data" using 1:(log10($2)) via b3,a3,c3



plot "out.plotB34vbv1.data" pt 7 ps 0.4 lc  rgb "red" title "Value by Value 1"\
,"out.plotB34vbvn.data" pt 7 ps 0.4 lc  rgb "orange" title "Value by Value n"\
,"out.plotB34cyclic.data" pt 7 ps 0.4 lc  rgb "green" title "Cyclic"\
,"out.plotB34vbv1.data" using ($1):((10**f(($1)))) with lines lt 3 lc rgb "red" lw 3 title sprintf ("Fit (N/{%.1f})^{%.1f}+{%.1f}",b1,a1,c1) \
,"out.plotB34vbvn.data" using ($1):((10**g(($1)))) with lines lt 3 lc rgb "orange" lw 3 title sprintf ("Fit (N/{%.1f})^{%.1f}+{%.1f}",b2,a2,c2) \
,"out.plotB34cyclic.data" using ($1):((10**h(($1)))) with lines lt 3 lc rgb "green" lw 3 title sprintf ("Fit (N/{%.1f})^{%.1f}+{%.1f}",b3,a3,c3) \
